package org.jboss.qa.examples.bpm_taxi;

import org.jboss.qa.examples.bpm_taxi.Address
import org.jboss.qa.examples.bpm_taxi.Taxi
import org.kie.api.runtime.process.WorkflowProcessInstance

declare DistanceToCustomer
    car: Taxi
    distanceInKm: double
end

rule 'Calculate distance from customer for an available taxi'
ruleflow-group 'nearestTaxi'
no-loop
when
  taxi : Taxi(inUse == false)
  drive : Drive(taxi == null, $drfrom: this.getFrom())
then
  // find distance
  Location taxiLocation = taxi.getCurrentLocation().getLocation();
  Location customerLocation = $drfrom.getLocation();
  System.out.println("driveFromLocation:" + customerLocation);
  double distance = taxiLocation.computeDistance(customerLocation);
  // infer additional knowledge
  DistanceToCustomer d = new DistanceToCustomer(taxi, distance / 1000.0);
  insert(d);
end

rule 'Find the taxi closest to the customer'
ruleflow-group 'nearestTaxi'
when
  fact: DistanceToCustomer($car: this.car)
  not DistanceToCustomer(this != fact, this.distanceInKm < fact.distanceInKm)
  drive: Drive(taxi == null)
then
  System.out.println("car:" + $car.getCurrentLocation());
  modify (drive) {
    setTaxi($car);
  }
end

rule 'Calculate taxi arrival time'
ruleflow-group 'calculatePrice'
when
  drive: Drive($car: taxi, arrivalTime == null)
  distance: DistanceToCustomer(car == $car)
then
  int minutesPerKm = 4;
  int travelTime = (int)(minutesPerKm * distance.getDistanceInKm());
  Instant travel = Instant.now().plus(travelTime, ChronoUnit.MINUTES);
  modify (drive) {
    setArrivalTime(Date.from(travel));
  }
  System.out.println("Travel time: " + travel);
end